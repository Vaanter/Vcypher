plugins {
  id 'java'
  id 'pmd'
  id "io.freefair.lombok" version "5.1.0"
  id "net.ltgt.errorprone" version "1.2.1"
  id "com.github.johnrengelman.shadow" version "6.0.0"
}

group 'com.vb'
version '1.1.0'

repositories {
  mavenCentral()
}

dependencies {
  implementation "com.google.guava:guava:29.0-jre"
  implementation 'com.google.flogger:flogger:0.5.1'
  implementation 'com.google.flogger:flogger-slf4j-backend:0.5.1'

  errorprone("com.google.errorprone:error_prone_core:2.4.0")

  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0-M1'
}

test {
  useJUnitPlatform()
}

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
  options.errorprone {
    disableWarningsInGeneratedCode = true
    ignoreUnknownCheckNames = true
    allErrorsAsWarnings = true
    // workaround for: https://github.com/google/error-prone/issues/780
    errorproneArgs = ['-Xep:ParameterName:OFF',
                      '-Xep:FallThrough:OFF',
                      '-Xep:FunctionalInterfaceClash:OFF',
                      '-Xep:AssertionFailureIgnored:OFF',
                      '-Xlint:unchecked',
                      '-Xlint:deprecation'
    ]
  }
}

shadowJar {
  archiveBaseName = 'Vcypher'
  classifier = ''
  manifest {
    attributes 'Main-Class': 'com.vb.vcypher.Main'
  }
}
